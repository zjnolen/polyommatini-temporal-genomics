from snakemake.utils import min_version


min_version("7.32.0")


module angsd:
    snakefile:
        "https://raw.githubusercontent.com/zjnolen/PopGLen/3c9453198b0a3f5ee08ddfc0df62ef2dff95b6bb/workflow/Snakefile"
    config:
        config


use rule * from angsd as angsd_*


include: "rules/ref_bias.smk"
include: "rules/call_genotypes.smk"
include: "rules/bcftools_roh.smk"
include: "rules/gerp.smk"
include: "rules/vep.smk"
include: "rules/theta_downsampling.smk"


wildcard_constraints:
    ref=config["reference"]["name"],
    dp=".{0}|.dp[1-9][0-9]*",
    population="|".join(
        ["all"]
        + ["all_excl_pca-admix"]
        + [i for i in angsd.samples.index.tolist()]
        + [i for i in angsd.samples.population.values.tolist()]
        + [i for i in angsd.samples.depth.values.tolist()]
    ),
    mindp="[1-9][0-9]*",


all_outputs = []

if config["vep"]:
    all_outputs.extend(
        expand(
            "results/datasets/{{dataset}}/analyses/vep/{{dataset}}.{{ref}}_all{dp}_{{sites}}-filts.filtered_mindp{mindp}-biallelic.{{trans}}.fmiss{{miss}}.varimpacts.tsv",
            zip,
            dp=["", ".dp5"],
            mindp=[6, 4],
        )
    )

if config["bcftools_roh"]:
    all_outputs.extend(
        expand(
            "results/datasets/{{dataset}}/plots/inbreeding/{{dataset}}.{{ref}}_all{dp}_{{sites}}-filts.filtered_mindp{mindp}-biallelic.{{trans}}.fmiss{{miss}}.bcftools.froh_bins.svg",
            zip,
            dp=["", ".dp5"],
            mindp=[6, 4],
        )
    )

if config["gerp_rel_load"]:
    all_outputs.extend(
        expand(
            "results/datasets/{{dataset}}/analyses/gerp/{{dataset}}.{{ref}}_all{dp}_{{sites}}-filts.filtered_mindp{mindp}-biallelic.{{trans}}.fmiss{{miss}}.relative-load-gerp.csv",
            zip,
            dp=["", ".dp5"],
            mindp=[6, 4],
        )
    )

if config["downsample_thetas"]:
    all_outputs.extend(
        expand(
            [
                "results/datasets/{{dataset}}/analyses/thetas/downsampled/{{dataset}}.{{ref}}_all{dp}.downsampled_{{sites}}-filts.thetaMean.{win}_{step}.tsv",
                "results/datasets/{{dataset}}/analyses/thetas/downsampled/{{dataset}}.{{ref}}_modern-historical{dp}.N4_{{sites}}-filts.wilcoxon.tsv",
            ],
            dp=[".dp2", ".dp3"],
            win=config["params"]["thetas"]["win_size"],
            step=config["params"]["thetas"]["win_step"],
        )
    )


rule all:
    input:
        rules.angsd_all.input,
        expand(
            all_outputs,
            sample=angsd.samples.index,
            ref=config["reference"]["name"],
            dataset=config["dataset"],
            sites=angsd.filters,
            trans=config["bcf_trans"],
            miss=config["bcf_missing"],
        ),
    default_target: True
